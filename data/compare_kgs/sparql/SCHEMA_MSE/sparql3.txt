# What are the softwares present in the MSE-KG?

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX nfdi: <https://nfdi.fiz-karlsruhe.de/ontology/>
PREFIX mwo: <http://purls.helmholtz-metadaten.de/mwo/>
PREFIX schema: <http://schema.org/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX swo: <http://www.ebi.ac.uk/swo/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
SELECT ?software ?softwareLabel ?licenseLabel ?languageLabel ?repositoryURL ?publicationLabel WHERE {
  ?software a nfdi:Software .
  OPTIONAL { ?software rdfs:label ?softwareLabel . }
  OPTIONAL { ?software swo:has_license ?license . ?license rdfs:label ?licenseLabel . }
  OPTIONAL { ?software nfdi:programmingLanguage ?language . ?language rdfs:label ?languageLabel . }
  OPTIONAL { ?software schema:citation ?publication . OPTIONAL { ?publication rdfs:label ?publicationLabel } }
  OPTIONAL {
    ?software mwo:hasRepository ?repo .
    OPTIONAL { ?repo nfdi:url ?repositoryURL . }
    OPTIONAL { ?repo mwo:hasWebsite ?repositoryURL . }
  }
}
LIMIT 999